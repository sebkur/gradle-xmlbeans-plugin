wrapper {
    gradleVersion = '5.2.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.gradle.plugin-publish'

group = pGroup
version = pVersion
description = pDescription

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") \
        + '/.gradle/topobyte.gradle'
}

buildscript {
    dependencies {
        classpath 'junit:junit:4.12'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'http://mvn.topobyte.de'
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.apache.xmlbeans:xmlbeans:3.1.0'

    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

pluginBundle {
  website = 'http://www.topobyte.de'
  vcsUrl = 'https://github.com/sebkur/gradle-xmlbeans-plugin'
  description = 'Topobyte XMLBeans plugin'

  plugins {
    xmlBeansPlugin {
      id = 'de.topobyte.xmlbeans-gradle-plugin'
      displayName = 'Topobyte XMLBeans plugin'
    }
  }
}

artifacts {
    archives sourcesJar
}

def pomInfo = new XmlSlurper().parse(file('info.pom'));

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name pomInfo.name
                    description pomInfo.description
                    url pomInfo.url
                    licenses {
                        license {
                            name pomInfo.licenseName
                            url pomInfo.licenseUrl
                            distribution pomInfo.licenseDistribution
                        }
                    }
                }
            }
        }
    }
}
